cd '.\ResponseFunction'
reset 

#------------------------------------ PREAMBLE
set autoscale
set term wxt size 800, 800 font "Helvetica,12"
set mxtics
set mytics

FORMAT = "set style line 12 lc rgb '#ddccdd' lt 1 lw 1.5; \
set style line 13 lc rgb '#ddccdd' lt 1 lw 0.5; \
set grid xtics mxtics ytics mytics back ls 12, ls 13;\
set size square;\
set xtics mirror;\
set ytics mirror;"
@FORMAT
#set logscale xy	
set style line 1 dt (3,5,10,5) lc rgb "black" lw 1.5
set style line 2 dt (3,3) lc rgb "red" lw 1.5
set style line 3 dt (8,8) lc rgb "blue" lw 1.5
set style line 4 dt (1,1) lc rgb "green" lw 1.5
		

#------------------------------------ Margins for each row resp. column
TMARGIN = "set tmargin at screen 0.91; set bmargin at screen 0.56"
BMARGIN = "set tmargin at screen 0.46; set bmargin at screen 0.11"
# TICS
NOYTICS = "set format y '';"
YTICS = "set format y '%g';"

#------------------------------------ PARAMETERS
L = 19; 
g = 0.72; 
h = 0.5;
rescale = 0							# rescale the spectral function by f(w, L)?
power = 0.5							# power in scaling with omega
SigX_or_SigZ = 1	 				# 0-SigX , 1-SigZ :local, 2-SpinCur
scaling = 0							# size scaling=1 or h-scaling=0
	h0 = 50;	hend = 200; dh = 10;
	L0 = 18;	Lend = 19; dL = 1;
##-- FIT FUNCTIONS
f(x) = a*x**b
set fit quiet


#------------------------------------ GRAPHICS

#------------------------------------ DATA AND FIT
data = sprintf("PowerLaw_L=%d,g=%.2f,k=0,p=1,x=1.dat", L, g)
set print data
x_min = 0.01; x_max = 0.1;
do for[hoo=h0:hend:dh]{
	hx = hoo/100.;
	operator = "SpinCur"
	if(SigX_or_SigZ < 2){
		operator = (SigX_or_SigZ == 0)? "SigmaX" : "SigmaZ"
	}
	if(SigX_or_SigZ == 0){
		if(hx < 0.8){
			x_min = 0.05; x_max = 0.3;
		}
		else {
			if(hx == 0.8){
				x_min = 0.02; x_max = 0.2;
			}
			else{
				x_min = 0.01; x_max = 0.1;
			}
		}
	}
	else{
		if(hx <= 0.7){
			x_min = 0.03; x_max = 0.5;
		}
		else{
			if(hx <= 1.0){
				x_min = 0.02; x_max = 0.1;
			}
			else{
				if(hx <= 1.6){
					x_min = 0.04; x_max = 0.2;
				}
				else{
					x_min = 0.01; x_max = 0.1;
				}
			}
		}
	}
	plotname = "ResponseFunction".operator.sprintf("_L=%d,g=%.2f,k=0,p=1,x=1,h=%.5f.dat", L, g, hx)
	a = 1; b = -1;
	fit[x_min:x_max] f(x) plotname u 1:2 via a,b
	print sprintf("%.2f\t\t%.8f\t\t%.8f\t\t%.8f\t\t%.8f", hx, a, a_err, b, b_err);
}

plot data u 1:4 w lp ls 1 title 'exponent', data u 1:4:5 w yerr title 'err'

cd '..\'